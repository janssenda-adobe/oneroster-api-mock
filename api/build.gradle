buildscript {
	ext {
		springBootVersion = '2.0.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}

}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.dm'
version = '1.0'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {

    compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-devtools")

	runtime('com.h2database:h2')
	testCompile('org.springframework.boot:spring-boot-starter-test')

	testCompile('com.intuit.karate:karate-junit4:0.7.0')
	testCompile('com.intuit.karate:karate-apache:0.7.0')
}

task unitTest ( type: Test ) {
	exclude 'features/**'
	testLogging {
		events = ["passed", "skipped", "failed"]
	}
}

task karateTest( type: Test ) {
	include 'features/**'
	testLogging {
		events = ["passed", "skipped", "failed"]
	}

	// pull cucumber options into the cucumber jvm
	systemProperty "cucumber.options", System.properties.getProperty("cucumber.options")
	// pull karate options into the jvm
	systemProperty "karate.env", System.properties.getProperty("karate.env")
	// ensure tests are always run
	outputs.upToDateWhen { false }

}
